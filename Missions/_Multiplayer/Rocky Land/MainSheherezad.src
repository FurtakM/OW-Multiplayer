EXPORT
  def_sheherezade_speed,
  def_sheherezade_stamina,
  def_sheherezade_skills,

  def_number_of_sheikhs,
  def_sheikhs_level,

  def_number_of_soldiers,
  def_soldiers_level,

  def_charges_allowed,

  def_amount_of_apemen,
  def_apemen_level,
  def_apemen_abilities,

  def_number_of_mastodons,
  def_number_of_tigers,

  def_time_to_prepare,
  def_flags;

EXPORT
    shzd_speed,shzd_stamina,shzd_skills,

    nsh,sh_skills,

    nsol,sol_skills,

    nstart_ape,ape_respawning,ape_skills,

    nmast1,nmast2,nmast3,ntig,tig_respawning,

    prep_time,flags;

EXPORT
    coordinates, sheherezades, lastunit;

// multiplayer parameters
EXPORT Side_Count, Team_Count, Player_Side, Player_Team, Side_Teams, Teams;
EXPORT Side_Positions, Side_Nations, Side_Names, Team_Names, Team_Game;


// reads definitons of multiplayer game (or simulate it in editor version)
function read_multiplayer_and_game_parameters;
  var i;
  begin
// sides and teams definitions for real Multiplayer
    if MultiPlayer then
      begin
        Player_Side= mp_player_side;         // cislo strany za kterou hraje TENTO hrac
        Player_Team= mp_player_team;         // cislo tymu, za ktery hrac hraje

        Side_Positions= mp_sides_positions;  // =[1,0,3,2,0,4,0,0]; ... pro kazdou stranu jeji pozice, 0 - neucastni se
        Side_Teams=  mp_sides_teams;         // =[1,0,1,2,0,2,0,0]; ... pro kazou stranu jeji tym
        Side_Nations= mp_sides_nations;      // =[1,0,1,2,0,1,0,0]; ... pro kazdou stranu jeji narod

        {Positions= mp_positions;}          // =[1,4,3,6];         ... seznam stran na jednotlivych pozicich, 0 - zadna (redundantni, not yet)
        Teams= mp_teams;                     // =[[1,3],[4,6]];     ... seznam seznamu clenu tymu (redundantni)
      end
    else
// sides and teams definitions for testing in editor
      begin
        Player_Side    = 1;
        Player_Team    = 0;

        Side_Positions = [1,2,3,4,5,0,0,0];
        Side_Teams     = [0,0,0,0,0,0,0,0];
        Side_Nations   = [2,2,2,2,2,2,2,2];

        {Positions   = [4,0,2,7];}
        Teams          = [];
      end;

// derived sides and teams informations
    Side_Count=(Side_Positions diff [0])+0;
    Team_Count=(Side_Teams diff [0])+0;

// game settings for real Multiplayer
    if MultiPlayer then
      begin
        def_sheherezade_speed=   GetMultiplayerSetting(0);
        def_sheherezade_stamina= GetMultiplayerSetting(1);
        def_sheherezade_skills=  GetMultiplayerSetting(2);

        def_number_of_sheikhs=   GetMultiplayerSetting(3);
        def_sheikhs_level=       GetMultiplayerSetting(4);

        def_number_of_soldiers=  GetMultiplayerSetting(5);
        def_soldiers_level=      GetMultiplayerSetting(6);

        def_charges_allowed=     GetMultiplayerSetting(7);

        def_amount_of_apemen=    GetMultiplayerSetting(8);
        def_apemen_level=        GetMultiplayerSetting(9);
        def_apemen_abilities=    GetMultiplayerSetting(10);

        def_number_of_mastodons= GetMultiplayerSetting(11);
        def_number_of_tigers=    GetMultiplayerSetting(12);

        def_time_to_prepare=     GetMultiplayerSetting(13);
        def_flags=               GetMultiplayerSetting(14);
      end
    else
// game settings for testing in editor
      begin
// not in multiplayer!!!
        randomize;
        randomizeAll;

        def_sheherezade_speed=   1;
        def_sheherezade_stamina= 1;
        def_sheherezade_skills=  2;

        def_number_of_sheikhs=   3;
        def_sheikhs_level=       2;

        def_number_of_soldiers=  3;
        def_soldiers_level=      1;

        def_charges_allowed=     3;

        def_amount_of_apemen=    2;
        def_apemen_level=        1;
        def_apemen_abilities=    1;

        def_number_of_mastodons= 2;
        def_number_of_tigers=    2;

        def_time_to_prepare=     2;
        def_flags=               2;
      end;

// derived game settings
    shzd_speed                  = [9,12,15] [def_sheherezade_speed+1];
    shzd_stamina                = [6,10,15] [def_sheherezade_stamina+1];
    shzd_skills                 = [[1,0,0,3],[2,0,0,5],[3,0,0,7]] [def_sheherezade_skills+1];

    nsh                         = [0,1,2,3,4,5] [def_number_of_sheikhs+1];
    sh_skills                   = [[2,1,1,1],[4,2,2,2],[6,3,3,3]] [def_sheikhs_level+1];

    nsol                        = [0,1,2,3,4,5] [def_number_of_soldiers+1];
    sol_skills                  = [[2,1,1,1],[4,2,2,2],[6,3,3,3]] [def_soldiers_level+1];

    nstart_ape                  = [0,2,4,6,8] [def_amount_of_apemen+1];
    // respawning time is divided by number of sides
    ape_respawning              = [0,4$00,2$00,1$00,0$30] [def_amount_of_apemen+1];
    ape_skills                  = [[1,0,0,0],[3,0,0,0],[5,0,0,0]] [def_apemen_level+1];

    // 1 - const, 2 - per side, 3 - percent per sheik
    nmast1                      = [0,2,4,6] [def_number_of_mastodons+1];
    nmast2                      = [0,1,2,3] [def_number_of_mastodons+1];
    nmast3                      = [0,15,30,45] [def_number_of_mastodons+1];
    ntig                        = [0,1,3,5] [def_number_of_tigers+1];
    // respawning time is divided by number of sides
    tig_respawning              = [0,8$00,4$00,2$00] [def_number_of_tigers+1];

    prep_time                   = [0,0$20,0$40,1$20] [def_time_to_prepare+1];
    flags                       = [0,25,50,75] [def_flags+1];

    if def_charges_allowed in [1,3] then
      for i:=1 to 8 do
        SetTech(tech_RemCharge,i,state_researched);
    if def_charges_allowed in [1,3] then
      for i:=1 to 8 do
        SetTech(tech_Explos,i,state_researched);
    for i:=1 to 8 do
      begin
        SetTech(tech_ApeLang,i,state_disabled);
        SetTech(tech_SibContam,i,state_disabled);
      end;
  end;

function prepare_map_coordinates;
  var best,c,bestc,minv,i,j,n;
  begin
    best=0;

    for n=1 to 400 do
      begin
        c=[];
        for i=1 to side_count do
          c:=c^[RandHexArea(arstart,false)];

        minv=10000;
        for i=1 to side_count do
          for j=1 to side_count do
            if i<>j then
              begin
                if GetDistXY(c[i][1],c[i][2],c[j][1],c[j][2])<minv then
                  minv:=GetDistXY(c[i][1],c[i][2],c[j][1],c[j][2]);
              end;
        if minv>best then
          begin
            best:=minv;
            bestc:=c;
          end;
      end;
    coordinates:=[];
    for i=1 to 8 do
      if side_positions[i]=0 then
        coordinates:=coordinates^[[0,0]]
      else
        begin
          coordinates:=coordinates^[bestc[1]];
          bestc:=delete(bestc,1);
        end;
  end;

function set_attitudes;
var i, j;
begin
  if not Team_Game then
    begin
      for i=1 to 8 do
        for j=1 to 8 do
          if i<>j then
            SetAttitude(i,j,att_neutral,true)
          else
            SetAttitude(i,j,att_friend,true)
    end;
end;

function create_sheherezade(side);
  var s;
  begin

    uc_side:=side;
    uc_nation:=nation_arabian;

    hc_name='Sheherezade';
    hc_sex=sex_female;
    hc_class=class_scientistic;
    hc_basic_skills=shzd_skills;
    hc_skills=shzd_skills;
    hc_attr=[shzd_stamina,shzd_speed];
    hc_gallery='multi';
    hc_face_number=18;
    hc_importance=105;

    s=CreateHuman;

    PlaceUnitXYR(s,coordinates[side][1],coordinates[side][2],1,false);

    sheherezades=Replace(sheherezades,side,s);
  end;



function prepare_sides;
  var i;
  begin
    for i:=1 to 8 do
      if side_positions[i] then
        create_sheherezade(i);
    if prep_time=0 then
      sheherezade_start_the_fight;

  end;

function prepare_side_to_the_fight(side,materialization);
  var x,y,i,sh,sol;
  begin
    SetTech(tech_ApeLang,side,state_researched);

    x:=GetX(Sheherezades[side]);
    y:=GetY(Sheherezades[side]);
    coordinates:=Replace(coordinates,side,[x,y]);

    if flags then
      begin
        SetBattleFlag(side,x,y,
        [[bfo_defend_bonus_human,flags],
         [bfo_defend_bonus_vehicle,flags],
         [bfo_defend_bonus_building,flags],
         [bfo_height,flags+25]]);
      end;

    uc_side:=side;
    uc_nation:=nation_arabian;

    hc_name:='';
    hc_class:=class_desert_warior;
    hc_sex:=sex_male;
    hc_basic_skills:=sh_skills;
    hc_skills:=sh_skills;
    hc_attr:=[10,10];
    hc_importance:=0;
    hc_gallery:='';

    for i:=1 to nsol do
      begin
        sh:=CreateHuman;
        PlaceUnitXYR(sh,x,y,5,materialization);
      end;
    hc_name:='';
    hc_class:=class_soldier;
    hc_sex:=sex_male;
    hc_basic_skills:=sol_skills;
    hc_skills:=sol_skills;
    hc_attr:=[10,10];
    hc_importance:=0;
    hc_gallery:='';

    for i:=1 to nsh do
      begin
        sh:=CreateHuman;
        PlaceUnitXYR(sh,x,y,5,materialization);
      end;
  end;

function find_hidden_hex(ar);
  var i,j,seen;
  begin
    for i:=1 to 100 do
      begin
        repeat
          result:=RandHexArea(ar,false);
        until not HexInfo(result[1],result[2]);
        seen:=false;
        for j:=1 to 8 do
          if SeeXY(j,result[1],result[2]) then
            seen:=true;
        if not seen then
          exit;
      end;
    result:=0;
  end;

function create_ape;
  var coord,i;
  begin
    if lastunit>=1999 then
      exit;
    uc_nation:=nation_nature;
    uc_side:=0;

    hc_name:='';
    hc_class:=class_apeman;
    hc_sex:=1;
    hc_basic_skills:=ape_skills;
    hc_skills:=ape_skills;
    hc_attr:=[12,12];
    hc_importance:=0;
    hc_gallery:='';
    hc_agressivity:=Rand(-15,15);

    coord:=find_hidden_hex(arstart);
    if coord then
      begin
        lastunit:=CreateHuman;
        PlaceUnitXY(lastunit,coord[1],coord[2],false);
      end;
  end;

function prepare_apes;
  var i;
  begin
    for i:=1 to side_count*nstart_ape do
      begin
        create_ape;
      end;
  end;

every 0$1 trigger tick>prep_time marked 15 do
  var delay;
  begin
    if not ape_respawning then
      exit;
    delay:=ape_respawning div side_count;
    repeat
      wait(delay*Rand(50,150)div 100);
      delay:=delay*110 div 100;
//      msg('Ape '&(delay/0$1));
      create_ape;
    until false;
  end;

function create_tiger;
  var coord,i;
  begin
    if lastunit>=1999 then
      exit;
    uc_nation:=nation_nature;
    uc_side:=0;

    hc_name:='';
    hc_class:=class_tiger;
    hc_sex:=1;
    hc_basic_skills:=[2,2,2,2];
    hc_skills:=[2,2,2,2];
    hc_attr:=[12,12];
    hc_importance:=0;
    hc_gallery:='';
    hc_agressivity:=Rand(30,50);

    coord:=find_hidden_hex(arstart);
    if coord then
      begin
        lastunit:=CreateHuman;
        PlaceUnitXY(lastunit,coord[1],coord[2],false);
      end;
  end;

function prepare_tigers;
  var i;
  begin
    for i:=1 to side_count*ntig do
      begin
        create_tiger;
      end;
  end;

every 0$1 trigger tick>prep_time marked 15 do
  var delay;
  begin
    if not tig_respawning then
      exit;
    delay:=tig_respawning div side_count;
    repeat
      wait(delay*Rand(50,150)div 100);
      delay:=delay*110 div 100;
//      msg('Tiger '&(delay/0$1));
      create_tiger;
    until false;
  end;

export function sheherezade_start_the_fight;
  var i;
  begin
    for i:=1 to 8 do
      if side_positions[i] then
        prepare_side_to_the_fight(i,prep_time>0);
  end;

function create_mastodon;
  var coord,i;
  begin
    if lastunit>=1999 then
      exit;
    uc_nation:=nation_nature;
    uc_side:=0;

    vc_chassis:=31;

    coord:=find_hidden_hex(arstart);
    if coord then
      begin
        lastunit:=CreateVehicle;
        PlaceUnitXYR(lastunit,coord[1],coord[2],3,false);
      end;
  end;

function prepare_mastodons;
  var i;
  begin
    for i:=1 to nmast1+side_count*nmast2+side_count*nsh*nmast3 div 100 do
      begin
        create_mastodon;
      end;
  end;

export function starting_sheherezad;
  begin
    mp_selectmsg:=true;
    read_multiplayer_and_game_parameters;
    prepare_map_coordinates;
    set_attitudes;

    prepare_sides;
    prepare_apes;
    prepare_tigers;
    prepare_mastodons;

    your_side:=Player_Side;
    ResetFog;
    AnimateTrees(true);
    music_nat:=nation_arabian;
    CenterNowOnXY(coordinates[your_side][1],coordinates[your_side][2]);

  end;

