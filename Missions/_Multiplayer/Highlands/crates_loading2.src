var shipment,respawn,inqueue;

// selection of first kvadrant
export function init_shipments_and_respawning;
begin
  respawn:=Rand(1,8);
  inqueue:=[0,0,0,0,0,0,0,0];
end;

every 0$1+0$0.5 do
  var n,building_list,aktresp,b;
  begin
    if respawning_time_max=0 then
      exit;

    if respawn=8 then respawn:=1
    else respawn:=respawn+1;

    if Side_Positions[respawn] and not inqueue[respawn] then
      begin
        n:=FilterAllUnits([ [f_type, unit_human],                      // units - people
                          [f_side, respawn],                         // side - respawning side
                          f_not, [f_nation,nation_nature]            // not tamed apeman
                        ]);
        if n<number_of_people then
          begin
            aktresp:=respawn;
            inqueue:=Replace(inqueue,aktresp,true);
            enable;

            wait(rand(respawning_time_min,respawning_time_max));
            inqueue:=Replace(inqueue,aktresp,false);

            building_list = FilterAllUnits([[f_type,unit_building],[f_or,[f_btype,b_depot],[f_btype,b_warehouse]],[f_ok],[f_side,aktresp]]);
            if building_list = 0 then
                 exit;  //won't respawn

            hc_name:='';
            hc_importance:=0;
            PrepareHuman(0,0,initial_level div 2);
            uc_nation:=Side_Nations[aktresp];
            uc_side:=aktresp;

            b:=building_list[Rand(1,building_list+0)];
            if GetBType(b) in [b_depot,b_warehouse] then
                 n = 25
            else
                 n = 10;

            PlaceUnitXYR(CreateHuman,GetX(b),GetY(b),n,true);
          end
        else
          enable;
      end
    else
      enable;
  end;

every 1$0+0$0.3 do  // crates anywhere on map
begin
 CreateCratesAnywhere(Rand(4,5),true);
 enable;
end;

every 0$1+0$0.6 do // create crates only on occupied bases
var i, amount;
begin
  case tick of
         1.. 1$0:  Wait(shipments_density/100*Rand(0$10,0$30));
     1$0+1.. 5$0:  Wait(shipments_density/100*Rand(0$30,0$45));
     5$0+1..10$0:  Wait(shipments_density/100*Rand(0$45,1$0));
    10$0+1..20$0:  Wait(shipments_density/100*Rand(1$0,1$15));
    20$0+1..30$0:  Wait(shipments_density/100*Rand(1$15,1$30));
    30$0+1..50$0:  Wait(shipments_density/100*Rand(1$30,1$45));
    50$0+1..300$0: Wait(shipments_density/100*Rand(1$45,2$0));
  else  Wait(shipments_density/100*Rand(2$0,2$30));
  end;

  amount:= Rand(3, 5);
  for i in (Side_Positions diff [0]) do
      CreateCratesArea(amount, [base_nw, base_ne, base_sw, base_se][i], true);

  enable;
end;

every 1$0 trigger def_shipments_density do
begin
  CreateCratesArea(5,map_center,true);
  wait(3$0-(1$0*def_shipments_density));
  enable;
end;
