// Apemans Spawning Script By Serpent 2020
// Amount_of_apemen: 0 - null, 1 - low (3 per player), 2 - medium (6 per player), 3 - high (9 per player)
// Spawn new ape when:
// - Apeman, apeman_soldier, apeman_engineer, apeman_kamikaze dies
// - Vehicles with ape control dies


// Increase level
Every 20$00 trigger apemanNumber do
begin
enable;

if apeLevel >= 8 then
   disable
else
   apeLevel := apeLevel + 1;
End;

// Respawn new ape
Every 2$30 trigger apemanNumber < apemanLimit do
var i, ape, pos, j;
begin
enable;

for i := 1 to apemanPos do
    begin
    if apemanPlayerCount[i] then
       begin
       if apemanPlayerCount[i] > apemanPos[i] then
          begin
          ape := PrepareApeman;
          PlaceUnitArea(ape, apeAreas[i], false);
          apemanPos := Replace(apemanPos, i, apemanPos[i] union ape);
          end;
       end;
    end;
End;


// Init settings
Export apemanNumber, apemanLimit, apemanPerPlayer, apemanPos, apemanPlayerCount, apeLevel, apeAreas;
Export Function InitApeman;
var i, playersAmount, j, ape, pos, _apeAreas;
begin
playersAmount := 0;
_apeAreas := [
  apeman_north,
  apeman_east,
  apeman_west,
  apeman_south
];
apeAreas := [0, 0, 0, 0, 0, 0, 0, 0];

for j = 1 to Side_Positions do
    if Side_Positions[j] then
       begin
       playersAmount := playersAmount + 1;
       apeAreas := Replace(apeAreas, j, _apeAreas[Side_Positions[j]]);
       end;

apemanPerPlayer := 3 * amount_of_apemen;
apemanNumber := playersAmount * apemanPerPlayer;
apemanLimit := apemanNumber;
apemanPos := [0, 0, 0, 0, 0, 0, 0, 0];
apemanPlayerCount := [0, 0, 0, 0, 0, 0, 0, 0];
apeLevel := 1;

if not apemanNumber then
   exit;

for i := 1 to apemanPerPlayer do
    begin
    for j := 1 to Side_Positions do
        if Side_Positions[j] then
           begin
           pos := Side_Positions[j];
           apemanPlayerCount := Replace(apemanPlayerCount, j, apemanPlayerCount[j] + 1);

           ape := PrepareApeman;
           apemanPos := Replace(apemanPos, j, Replace(apemanPos[j], apemanPos[j] + 1, ape));
           PlaceUnitArea(ape, apeAreas[j], false);
           end;
    end;
End;

Export Function PrepareApeman;
var bonus;
begin
uc_side := 0;
uc_nation := 0;

InitHc;

bonus := [-1, 0, 1][FilterAllUnits([f_class, class_apeman]) mod 3 + 1];

hc_class := class_apeman;
hc_gallery := '';
hc_name := '';
hc_skills := [apeLevel + bonus, apeLevel + bonus, apeLevel + bonus, apeLevel + bonus];
hc_agressivity := 10 * bonus;
hc_attr := [12, 12];

result := CreateHuman;
End;

Export Function ApeKilled(ape);
var side, i;
begin
if not ape then
   exit;

side := GetSide(ape);

if not side then
   begin
   for i = 1 to apemanPos do
       if ape in apemanPos[i] then
          begin
          side := i;
          break;
          end;
   end;

apemanPos := Replace(apemanPos, side, apemanPos[side] diff ape);
apemanNumber := apemanNumber - 1;
End;

