// Positions on the map
//   1 2 3
//   6 5 4

EXPORT
  def_base_level,         // (0)  No Base, Depot Only, Fortified, *Extra Fortified
  def_amount_of_people,   // (1)  9, 12, 15, 24
  def_skill_level,        // (2)  Unexperienced, Little Experienced, Experienced, More Experienced
  def_starting_resources, // (3)  None, Few, Medium, Lots
  def_shipments_density,  // (4)  Low, Medium, High, Very High
  def_extra_oil_deposits, // (5)  Normal, Extra / *Bool
  def_extra_sib_deposits, // (6)  None, Normal, Extra / *-
  def_shared_vision,      // (10) True, False
  def_morale_flags,       // (11) None, Fast Decreasing, Decreasing, Slowly Decreasing, Not Decreasing
  def_siberite_detection, // (12) True, False
  def_people_respawning,  // (13) None, Rare, Common
  def_amount_of_apemen,   // (14) None, Few, Medium, Lots
  def_amount_of_siberit,  // (15)
  def_public_score,       // (16)
  def_siberite_bomb,      // (17)
  def_builduptime;        // (20)

EXPORT
  No_Mechs,
  Game_Type;              // Types of used games
                          // 1 - Kill 'em all
                          // 2 - Team war
                          // 3 - East vs West

EXPORT
  is_depot,
  number_of_breastworks,
  number_of_people,
  skill_level,
  starting_crates,
  starting_resources,
  shipments_density,
  morale_decreasing_time,
  amount_of_apemen,
  amount_of_siberit,
  respawning_time_min,
  respawning_time_max,
  spect, def_spect,
  siberite_bomb_Delay,
  def_win_rules,
  tech_level;

export  Side_Teams, Teams, Side_Comps;
export  Side_Positions, Side_Nations, Team_Game;

export  depot_def, breastworks_def, start_def,
        normal_oil_deposits_locations,
        extra_oil_deposits_locations,
        normal_sib_deposits_locations,
        extra_sib_deposits_locations,
        battle_flags_id;

export heroes;
var commander;

var global_units,
    global_defined,
    soldiers_created;

var neng,
    nsci,
    nmech,
    nsol;

function mp_sides_comps;
var c,i;
begin
  c = [];
  for i=1 to 8 do
   c = c^Multiplayer_GetPlayerIsComp(i);
  result = c;
end;

function read_multiplayer_and_game_parameters;
var i;
begin
  if Multiplayer then
    begin
      Game_Type:=      mp_game_type;

      Your_Side:=      mp_player_side;

      Side_Positions:= mp_sides_positions;
      Side_Teams:=     mp_sides_teams;
      Side_Nations:=   mp_sides_nations;
      Side_Comps:=     mp_sides_comps;

      Teams:=          mp_teams;
    end
  else
    begin
      Game_Type:=      1;

      Your_Side:=      1;

      Side_Positions:= [1,2,3,4,5,6,0,0];
      Side_Teams:=     [1,2,3,4,5,6,0,0];
      Side_Nations:=   [1,2,3,1,2,3,0,0];
      Side_Comps:=     [0,0,0,0,0,0,0,0];

      Teams:=          [[1],[2],[3],[4],[5],[6]];
    end;

  Team_Game:= (Side_Teams diff [0]) > 1;

  if Multiplayer then
    begin
      def_base_level:=         GetMultiplayerSetting(0);
      def_amount_of_people:=   GetMultiplayerSetting(1);
      def_skill_level:=        GetMultiplayerSetting(2);
      def_starting_resources:= GetMultiplayerSetting(3);
      def_shipments_density:=  GetMultiplayerSetting(4);
      def_extra_oil_deposits:= GetMultiplayerSetting(5);
      def_extra_sib_deposits:= GetMultiplayerSetting(6);
      def_shared_vision:=      GetMultiplayerSetting(10);
      def_morale_flags:=       GetMultiplayerSetting(11);
      def_siberite_detection:= GetMultiplayerSetting(12);
      def_people_respawning:=  GetMultiplayerSetting(13);
      def_amount_of_apemen:=   GetMultiplayerSetting(14);
      def_amount_of_siberit:=  GetMultiplayerSetting(15);
      def_public_score:=       GetMultiplayerSetting(16);
      def_siberite_bomb:=      GetMultiplayerSetting(17);
      def_builduptime:=        GetMultiplayerSetting(20);
      def_win_rules:=          0;
      tech_level =             GetMultiplayerSetting(23);
    end
  else
    begin
      Randomize;

      def_base_level:=         2;
      def_amount_of_people:=   0;
      def_skill_level:=        1;
      def_starting_resources:= 2;
      def_shipments_density:=  1;
      def_extra_oil_deposits:= 0;
      def_extra_sib_deposits:= 1;
      def_shared_vision:=      1;
      def_morale_flags:=       1;
      def_siberite_detection:= 1;
      def_people_respawning:=  2;
      def_amount_of_apemen:=   3;
      def_amount_of_siberit:=  0;
      def_public_score:=       0;
      def_siberite_bomb:=      0;
      def_builduptime:=        1;
      def_win_rules:=          0;
      tech_level:=             0;
    end;   

  if isSGUI then
     setMpTechs()
  else
      SetMpTechLevel(tech_level + 1);

  is_depot                    := [0,1,1] [def_base_level+1];
  number_of_breastworks       := [0,0,3] [def_base_level+1];
  number_of_people            := [9,12,15,24] [def_amount_of_people+1];
  skill_level                 := [2,3,5,6] [def_skill_level+1];
  starting_resources          := [[0,0,0], [30,0,0], [50,20,0], [80,40,20]] [def_starting_resources+1];
  starting_crates             := [[10], [7,14], [6,12,18], [5,10,15,20]] [def_starting_resources+1];
  shipments_density           := [150, 125, 100, 50] [def_shipments_density+1];
  respawning_time_min         := [0$0, 5$00, 3$0] [def_people_respawning+1];
  respawning_time_max         := [0$0, 15$00, 5$0] [def_people_respawning+1];
  amount_of_apemen            := [0, 1, 2, 3] [def_amount_of_apemen+1];
  amount_of_siberit           := [30, 50, 80, 100, 130, 150, 200] [def_amount_of_siberit+1];
  morale_decreasing_time      := [ -1, 0$10, 0$20, 0$40, 0$0 ] [def_morale_flags+1];
  battle_flags_id             := [];
  spect                       := [0,1] [def_spect+1];
  siberite_bomb_Delay         := [0,1,30$00,60$00,90$00,120$00] [def_siberite_bomb+1];

end;

function init_map;
var i,j;
begin
  normal_oil_deposits_locations:=[ [12,4], [45,81], [116,195], [142,4], [201,102], [234,194] ];
  extra_oil_deposits_locations :=[ [[12,4],[16,25]], [[45,81],[60,102]], [[116,195],[92,167]], [[142,4],[167,24]], [[201,102],[190,77]], [[234,194],[237,171]] ]; // list of xtra oil deposits, [[88,28],[79,10]], [[136,177],[156,134]]

  normal_sib_deposits_locations:=[ [10,10], [51,93], [102,187], [158,6], [203,93], [252,194] ];
  extra_sib_deposits_locations :=[ [[10,10],[36,10]], [[51,93],[47,72]], [[102,187],[93,175]], [[158,6],[147,5]], [[203,93],[173,71]], [[252,194],[243,177]] ];

  depot_def:=  [
               [ 23, 15,2 ],
               [ 56, 85,1 ],
               [ 110,177,3 ],
               [ 157,13,3 ],
               [ 187,87,5 ],
               [ 231,181,5 ]
               ];

  // list of starting people positions
  start_def:=[ [30,22], [58,82], [113,180], [151,24], [183,90], [223,180]];

  // list of breastworks for each base positions on the map
  breastworks_def:=
                  [
                  [ [ 33, 12, 5], [ 42, 29, 4], [ 36, 20, 4] ],
                  [ [72, 91, 4], [ 65, 78, 4], [ 75, 100, 3] ],
                  [ [102, 165, 3], [122, 176, 4], [120, 184, 4] ],
                  [ [134, 16, 1], [132,5, 1], [157,27, 5] ],
                  [ [172,77, 1], [175,96, 1], [192,104, 1] ],
                  [ [216,179, 1], [208,160, 2], [208,171, 2] ]
                  ];

  for i=1 to 6 do
    if def_extra_oil_deposits then
      for j in extra_oil_deposits_locations[i] do
        CreateDepositXY(j[1],j[2],mat_oil)
    else
      CreateDepositXY(normal_oil_deposits_locations[i][1],normal_oil_deposits_locations[i][2],mat_oil);

  if def_extra_sib_deposits then
    for i=1 to 6 do
      if def_extra_sib_deposits=2 then
        for j in extra_sib_deposits_locations[i] do
          CreateDepositXY(j[1],j[2],mat_siberit)
      else
        CreateDepositXY(normal_sib_deposits_locations[i][1],normal_sib_deposits_locations[i][2],mat_siberit);
end;

function set_shared_visions_and_alliances;
var i, j;
begin
  if not Team_Game then
    begin
      for i=1 to 8 do
        for j=1 to 8 do
          if i<>j then
            SetAttitude(i,j,att_neutral,true)
          else
            SetAttitude(i,j,att_friend,true);
    end
  else
    begin
      if def_shared_vision then
        begin
          for i in Teams do
            for j=2 to i do
              ChangeSideFog( i[j], i[1] );
        end;

      for i=1 to 8 do
        for j=1 to 8 do
          if Side_Teams[i]=Side_Teams[j] then
            SetAttitude(i,j,att_friend,true)
          else
            SetAttitude(i,j,att_enemy,true);
    end;
end;

function prepare_people(count,cl,x,y,r,building);
var i,j,un;
begin
  result:=[];
  for i:=1 to count do
    begin
      uc_direction:=Rand(0,5);
      if commander then
        begin
          hc_name := mp_sides_players_names[uc_side];
          PrepareHuman(Rand(1,2),cl,skill_level+2);
          hc_importance:=105;
          commander:=false;
          If Multiplayer then
            begin
              hc_gallery:= 'MULTIAVATARS';
              hc_face_number:=Multiplayer_GetPlayerSideNum(uc_side);
              hc_sex := Multiplayer_GetPlayerSex(uc_side);
            end;
        end
      else
        begin
          hc_name := '';
          PrepareHuman(rand(1,2),cl,skill_level);
          hc_importance:=0;
          If Multiplayer then
            begin
              hc_gallery:= '';
              hc_face_number:=0;
            end;
        end;

      un:=CreateHuman;

      if global_defined then
        begin
          if (cl = class_soldier) then
            j:= i + soldiers_created
          else
            j:= i;
          CopySkills(global_units[cl][j],un);
          SetAttr(un, attr_speed,   GetAttr( global_units[cl][j], attr_speed)   );
          SetAttr(un, attr_stamina, GetAttr( global_units[cl][j], attr_stamina) );
        end;

      if building then
        PlaceHumanInUnit(un, building)
      else
        PlaceUnitXYR(un, x, y, r, false);

      result:=result ^ [un];
    end;

  if not global_defined then
    if (global_units >= cl) then
      global_units:= Replace(global_units, cl, global_units[cl] ^ result)
    else
      global_units:= Replace(global_units, cl, result);
end;

function prepare_side(side);
var pos,x,y,dir,i,r,br,dep;
begin
  if side in [0,9] then
     exit;

  pos:=Side_Positions[side];

  uc_nation:=Side_Nations[side];
  uc_side:=side;

  x:=depot_def[pos][1];
  y:=depot_def[pos][2];
  dir:=depot_def[pos][3];

  if is_depot then
    begin
      bc_type=b_depot;
      dep:=CreateAndPlaceBuildingXYD(x,y,dir);
      SetBName(dep,'@' & side);

      SetResourceType(GetBase(dep),mat_cans,starting_resources[mat_cans]);
      SetResourceType(GetBase(dep),mat_oil,starting_resources[mat_oil]);
      SetResourceType(GetBase(dep),mat_siberit,starting_resources[mat_siberit]);
    end
  else
    begin
      for i:=1 to 3 do
        begin
          r:=starting_resources[i];
          while r>=10 do
            if r>50 then
              begin
                CreateResourcesXYR(i,5,x,y,4,false);
                r:=r-50;
              end
            else
              begin
                CreateResourcesXYR(i,r div 10,x,y,4,false);
                r:=0;
              end;
         end;
    end;

  for r in starting_crates do
    CreateResourcesXYR(mat_cans,5,x,y,r,false);

  if def_morale_flags then
    begin
      x:=start_def[pos][1];
      y:=start_def[pos][2];
      PlaceEnvironment(x,y,12,2);
      battle_flags_id:=battle_flags_id ^ SetBattleFlag(side,x,y,
                                                       [ [bfo_defend_bonus_human,100],
                                                         [bfo_defend_bonus_vehicle,50],
                                                         [bfo_defend_bonus_building,20],
                                                         [bfo_height,100] ]);
    end;

  commander:=true;

  soldiers_created:=0;
  for i:=1 to number_of_breastworks do
    begin
      x:=breastworks_def[pos][i][1];
      y:=breastworks_def[pos][i][2];
      dir:=breastworks_def[pos][i][3];

      bc_type:=b_breastwork;
      br:=CreateAndPlaceBuildingXYD(x,y,dir);

      prepare_people(1,class_soldier,0,0,0,br);
      soldiers_created:=soldiers_created+1;
    end;

  x:=start_def[pos][1];
  y:=start_def[pos][2];

  prepare_people(nsol-number_of_breastworks,class_soldier,x,y,5,0);
  prepare_people(neng,class_engineer,x,y,5,0);
  prepare_people(nmech,class_mechanic,x,y,5,0);
  prepare_people(nsci,class_scientistic,x,y,5,0);

  if def_siberite_detection then
    SetTech(tech_SibDet,side,state_researched);

  if def_siberite_bomb = 1 then
  begin
    SetTech(tech_SibFiss,side,state_enabled);
    SetTech(73,side,state_enabled);
  end
  else
  begin
    SetTech(tech_SibFiss,side,state_disabled);
    SetTech(73,side,state_disabled);
  end;

  global_defined:= true;
end;

every 0$1 do
if (def_siberite_bomb > 1) then
begin
    wait( siberite_bomb_Delay );  // can't be 0
    EnableSibFiss;
end;

function EnableSibFiss;
var i;
begin
  for i:=1 to 8 do
    if Side_Positions[i] then
    begin
      SetTech(tech_SibFiss,i,state_enabled);
      SetTech(73,i,state_enabled);
    end;
end;

function prepare_sides;
var i;
begin
  global_units:=   [];
  global_defined:= False;

  CountClassMembers;

  for i:=1 to 8 do
    if Side_Positions[i] then
      prepare_side(i);
end;

function CountClassMembers;
var Mode;
begin
  if No_Mechs then
    Mode:= 2
  else
    Mode:= 1;

  neng:= number_of_people;
  nsol:= neng div [4, 3] [Mode];

  if (nsol < number_of_breastworks) then
    nsol:= number_of_breastworks;

  neng:= neng - nsol;

  nsci:= neng div [3, 2] [Mode];

  if (nsci < 1) then
    nsci:= 1;

  neng:= neng - nsci;

  nmech:= [neng div 2, 0] [Mode];

  neng:= neng - nmech;

  if (neng < 1) then
    neng:= 1;
end;

function Init_Win_Condition;
begin
  if Team_Game then
    Enable(98)
  else
    Enable(99);
  case Game_Type of
    3: Enable(100);
  end;
end;

starting
begin
  Disable(98);  // YouWin (Team war)
  Disable(99);  // YouWin (Kill 'em all)

  Disable(400); // SuddenLost
  Disable(401); // SuddenWin  

  mp_selectmsg := true;

  read_multiplayer_and_game_parameters;
  init_map;
  init_respawning;
  set_shared_visions_and_alliances;
  prepare_sides;

  ShowTimer;

  music_nat:=Side_Nations[your_side];

  if IAmSpec then
    begin
      PlaceSeeing(0, 0, Your_Side, -32767);
      CenterNowOnXY(104, 69);
      Enable(400);
    end
  else
    begin
      ResetFog;
      CenterNowOnXY(start_def[Side_Positions[your_side]][1],start_def[Side_Positions[your_side]][2]);
      Init_Win_Condition;
    end;

  AnimateTrees(true);
  InitApeman;

  BuildUpAreas:= [BuildUp1, BuildUp2, BuildUp3, BuildUp4, BuildUp5, BuildUp6];
  BeginBuildUp;
end;