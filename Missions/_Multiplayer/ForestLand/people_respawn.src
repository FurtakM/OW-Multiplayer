var respawn_queue;

export function init_respawn;
begin
  if not def_people_respawning then
    exit;

  // side         1 2 3 4 5 6 7 8
  respawn_queue:=[0,0,0,0,0,0,0,0];      // if side want new character  (0/TIME)
end;                                         

every 0$10 trigger def_people_respawning do
var un,list,time,side;                             
begin
  for side=1 to Side_Positions do
    if Side_Positions[side] then
      begin
        list:=FilterAllUnits([ [f_side,side], [f_type,unit_human], f_not, [f_nation,nation_nature] ]);
        if (list<number_of_people) and (not respawn_queue[side]) then
          set_queue(side,TICK);

        time:=TICK-respawn_queue[side];
        if (respawn_queue[side]) and (time>respawning_time_min) then
          respawn_character(side,time);
      end;
  enable;
end;

function respawn_character(side,time);
var dep;
begin
  if Rand(0,1) and (time<respawning_time_max) then
    exit;

  dep:=FilterAllUnits([ [f_side,side], [f_or, [f_btype,b_depot], [f_btype,b_warehouse]], [f_ok] ]);
  if not dep then
    exit
  else
    dep:=dep[Rand(1,dep)];

  uc_side:=side;
  uc_nation:=Side_Nations[side];
  PrepareHuman(0,0,initial_level div 2);
  hc_importance:=0;

  PlaceUnitXYR(CreateHuman,GetX(dep),GetY(dep),10,true);
  set_queue(side,0);
end;


function set_queue(side,time);
begin
  respawn_queue:=Delete(respawn_queue,side);
  respawn_queue:=Insert(respawn_queue,side,time);
end;
