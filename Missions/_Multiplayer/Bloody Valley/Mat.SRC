var shipment,respawn,inqueue;

// selection of first kvadrant
export function init_shipments_and_respawning;
  begin
    shipment:=Rand(0,3);
    respawn:=Rand(1,8);
    inqueue:=[0,0,0,0,0,0,0,0];
  end;

every 0$1+0$0.5 do
  var n,building_list,aktresp,b;
  begin
    if respawning_time_max=0 then
      exit;
                                           
    if respawn=8 then respawn:=1
    else respawn:=respawn+1;

    if Side_Positions[respawn] and not inqueue[respawn] then
      begin
        n:=FilterAllUnits([ [f_type, unit_human],                      // units - people
                          [f_side, respawn],                         // side - respawning side
                          f_not, [f_nation,nation_nature]            // not tamed apeman
                        ]);
        if n<number_of_people then
          begin
            aktresp:=respawn;
            inqueue:=Replace(inqueue,aktresp,true);
            enable;

            wait(rand(respawning_time_min,respawning_time_max));
            inqueue:=Replace(inqueue,aktresp,false);

            building_list = FilterAllUnits([[f_type,unit_building],[f_or,[f_btype,b_depot],[f_btype,b_warehouse]],[f_ok],[f_side,aktresp]]);
            if building_list = 0 then
                 building_list = FilterAllUnits([[f_type,unit_building],[f_ok],[f_side,aktresp]]);
            if building_list = 0 then
                 exit;  //won't respawn

            hc_name:='';
            hc_importance:=0;
            PrepareHuman(0,0,skill_level div 2);
            uc_nation:=Side_Nations[aktresp];
            uc_side:=aktresp;

            b:=building_list[Rand(1,building_list+0)];
            if GetBType(b) in [b_depot,b_warehouse] then
                 n = 25
            else
                 n = 10;

            PlaceUnitXYR(CreateHuman,GetX(b),GetY(b),n,true);
          end
        else
          enable;
      end
    else
      enable;
  end;

every 1$0+0$0.3 do  // crates anywhere on map
begin
 CreateCratesAnywhere(Rand(4,5),true);
 enable;
end;

every 0$1+0$0.6 do // create crates only on occupied bases
var i, amount;
begin
  case tick of
         1.. 1$0:  Wait(shipments_density/100*Rand(0$10,0$20));
     1$0+1.. 5$0:  Wait(shipments_density/100*Rand(0$20,0$35));
     5$0+1..10$0:  Wait(shipments_density/100*Rand(0$35,0$50));
    10$0+1..20$0:  Wait(shipments_density/100*Rand(0$50,1$10));
    20$0+1..30$0:  Wait(shipments_density/100*Rand(1$10,1$20));
    30$0+1..50$0:  Wait(shipments_density/100*Rand(1$20,1$35));
    50$0+1..300$0: Wait(shipments_density/100*Rand(1$35,2$0));
  else  Wait(shipments_density/100*Rand(2$0,2$30));
  end;

  amount:= Rand(1, 5);

  for i = 1 to 2 do
    CreateCratesArea(amount, BuildUpAreas[i], true);

  enable;
end;